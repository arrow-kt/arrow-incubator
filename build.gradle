buildscript {
    ext {
        def customProperties = new Properties()
        def customPropertiesURL = new URL("$PROPERTIES")
        def customPropertiesStream = customPropertiesURL.openStream()
        customProperties.load(customPropertiesStream)
        customProperties.each { key, value -> set(key, value) }
        apply from: "$COMMON_SETUP"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$KOTLIN_VERSION"
    id "org.jetbrains.kotlin.kapt" version "$KOTLIN_VERSION"
    id "org.jlleitschuh.gradle.ktlint" version "$KTLINT_GRADLE_VERSION"
    id "org.jetbrains.dokka" version "$DOKKA_VERSION"
    id "ru.vyarus.animalsniffer" version "$ANIMALS_SNIFFER_VERSION" apply false
}

apply from: "$ROOT_PROJECT"

subprojects {
    // -- Gradle Animal Sniffer Plugin: https://github.com/xvik/gradle-animalsniffer-plugin
    apply plugin: 'ru.vyarus.animalsniffer'
    apply plugin: 'java'
    animalsniffer {
        ignore 'java.lang.*'
    }
    dependencies {
        signature 'net.sf.androidscents.signature:android-api-level-21:5.0.1_r2@signature'
    }

    dokka {
        outputFormat = 'jekyll'
        outputDirectory = "${rootDir}/build/apidocs"
        configuration {
            includes = ['README.md']
            reportUndocumented = false
            sourceLink {
                path = "src/main/kotlin"
                url = "https://github.com/arrow-kt/arrow/blob/main/${relativeProjectPath('src/main/kotlin')}"
                lineSuffix = "#L"
            }
        }
    }
}
